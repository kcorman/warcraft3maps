package Loot
import ClosureEvents
import PTQConstants
import GameInit
import LinkedList
import TimerUtils
import CustomAbilities
import GameTimer
import ClosureTimers
constant real TEXT_TAG_TICK_TIME = .25
int ITEM_LUMBER = 'lmbr'
int ITEM_GOLD = 'gold'

class TagWrapper
    texttag tag
    real liveUntil

// could keep this sorted for a slight optimization if we have lots of tags, but I dont think its necessary
LinkedList<TagWrapper> textTags = new LinkedList<TagWrapper>

function setTextTagTime(texttag tag, real duration)
    TagWrapper wrap = new TagWrapper()
    wrap.tag = tag
    wrap.liveUntil = getElapsedGameTime() + duration
    textTags.add(wrap)

init
    getTimer().doPeriodically(TEXT_TAG_TICK_TIME) (CallbackPeriodic cb) ->
        LLIterator<TagWrapper> itr = textTags.iterator()
        let curTime = getElapsedGameTime()
        while(itr.hasNext())
            let tag = itr.next()
            if(tag.liveUntil < curTime)
                tag.tag.destr()
                itr.remove()
    
    EventListener.add(EVENT_PLAYER_UNIT_DEATH) () ->
        let dyingUnit = GetDyingUnit()
                    //setTextTagTime(t, 2)
        if(dyingUnit.hasAbility(ABILITY_GIVES_BOSS_LOOT))
            let baseGold = dyingUnit.getLevel() * 100
            for player p in Utils.allPlayers
                p.addGold(baseGold)
                createTTEx(dyingUnit.getPos3Real(), vec2(0, .03), "BOSS +" + baseGold.toString(), 10, 2, colorA(255, 215, 0,0))
        else if(dyingUnit.hasAbility(ABILITY_GIVES_LOOT))
            if(GetRandomReal(0, 1.) > .96)
                createItem(ITEM_LUMBER, GetDyingUnit().getPos())
            else if (GetRandomReal(0, 1) > .96)
                createItem(ITEM_GOLD, GetDyingUnit().getPos())

            if(dyingUnit.getOwner() == BadGuy)
                let baseGold = dyingUnit.getLevel() * 10
                let perPersonGold = (dyingUnit.getLevel() * 10 / Utils.allPlayers.size().toReal()).toInt()
                let totalGold = baseGold + perPersonGold
                for player p in Utils.allPlayers
                    p.addGold(totalGold)
                    createTTEx(dyingUnit.getPos3Real(), vec2(0, .03), "+" + totalGold.toString(), 10, 2, colorA(255, 215, 0,0))


 